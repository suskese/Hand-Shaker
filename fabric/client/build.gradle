plugins {
	id 'fabric-loom'
	id 'maven-publish'
}

version = mod_version
group = maven_group

base {
	archivesName = "${archives_base_name}-fabric-client"
}

repositories {
	mavenCentral()
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	
}


processResources {
	def version = project.version
	def supportedVersions = supported_mc_versions
	filesMatching("fabric.mod.json") {
		filter { line ->
			line.replace('${version}', version)
				.replace('${supported_mc_versions}', supportedVersions)
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.named('sourcesJar').configure {
	duplicatesStrategy = 'exclude'
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = archives_base_name
			from components.java
		}
	}

	repositories {

	}
}

afterEvaluate {
    def envFile = rootProject.file('.env')
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            def (key, value) = line.split('=', 2)
            if (key && value && !System.getenv(key)) {
                System.setProperty(key, value)
            }
        }
    }

    tasks.register('signFabricJar', Exec) {
        group = 'build'
        description = 'Signs the remapped Fabric client JAR.'
        dependsOn(tasks.named('remapJar'))

        def remappedJarProvider = tasks.named('remapJar').flatMap { it.outputs.files.elements.map { fs -> fs.first().asFile } }
        def signedJar = layout.buildDirectory.file("libs/hand-shaker-fabric-client-${rootProject.version}-signed.jar")

        inputs.file(remappedJarProvider)
        inputs.file(rootProject.file('.Integrity/keystore.jks'))
        outputs.file(signedJar)

        executable = 'jarsigner'

        doFirst {
            def jarToSign = remappedJarProvider.get()
            if (rootProject.file('.Integrity/keystore.jks').exists()) {
                println "--> Signing JAR: ${jarToSign.name}"
                def keystorePassword = System.getenv('KEYSTORE_PASSWORD') ?: System.getProperty('KEYSTORE_PASSWORD') ?: 'password'
                args = [
                    '-keystore', rootProject.file('.Integrity/keystore.jks').absolutePath,
                    '-storepass', keystorePassword,
                    '-signedjar', signedJar.get().asFile.absolutePath,
                    jarToSign.absolutePath,
                    'handshaker'
                ]
            } else {
                println "--> Keystore not found in Integrity/keystore.jks. Skipping signing."
                enabled = false
            }
        }
    }

    tasks.named('assemble').configure {
        dependsOn('signFabricJar')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-Xlint:deprecation")
}
