plugins {
    id 'base'
}

version = mod_version
group = maven_group

tasks.register('copyCert') {
    doLast {
        if (findProject(':common') != null) {
            copy {
                from '.Integrity/public.cer'
                into 'common/src/main/resources'
            }
        }
    }
}

tasks.register('copyArtifacts') {
    def projectList = [
        'fabric:1.21.11',
        'fabric:1.21.10',
        'neoforge:1.21.10',
        'neoforge:1.21.11',
        'paper',
        'velton'
    ]
    
    def projects = [:]
    def libDirs = [:]
    
    projectList.each { projectPath ->
        def proj = findProject(':' + projectPath)
        if (proj != null) {
            projects[projectPath] = proj
            libDirs[projectPath] = proj.layout.buildDirectory.dir('libs')
            dependsOn proj.tasks.named('assemble')
        }
    }

    doLast {
        def copyConfigs = [
            'fabric:1.21.11': '*-s.jar',
            'fabric:1.21.10': '*-s.jar',
            'neoforge:1.21.10': '*.jar',
            'neoforge:1.21.11': '*.jar',
            'paper': '*',
            'velton': '*-signed.jar'
        ]
        
        projects.each { projectPath, proj ->
            copy {
                from libDirs[projectPath]
                into layout.buildDirectory.dir('output')
                include copyConfigs[projectPath]
            }
        }
    }
}

tasks.named('build') {
    dependsOn 'copyArtifacts'
}