plugins {
    id 'base'
}

version = mod_version
group = maven_group

tasks.register('copyCert') {
    doLast {
        if (findProject(':paper') != null) {
            copy {
                from '.Integrity/public.cer'
                into 'paper/src/main/resources'
            }
        }
        if (findProject(':fabric:client') != null) {
            copy {
                from '.Integrity/public.cer'
                into 'fabric/common/resources'
            }
        }
        if (findProject(':neoforge:server') != null) {
            copy {
                from '.Integrity/public.cer'
                into 'neoforge/server/src/main/resources'
            }
        }
        if (findProject(':velton') != null) {
            copy {
                from '.Integrity/public.cer'
                into 'velton/src/main/resources'
            }
        }
    }
}

tasks.register('copyArtifacts') {
    def projectList = [
        'fabric:1.21.1',
        'fabric:1.21.5',
        'fabric:1.21.10',
        'fabric:1.21.11',
        'fabric:client',
        'neoforge:server',
        'neoforge:client',
        'paper',
        'velton'
    ]
    
    def projects = [:]
    def libDirs = [:]
    
    projectList.each { projectPath ->
        def proj = findProject(':' + projectPath)
        if (proj != null) {
            projects[projectPath] = proj
            libDirs[projectPath] = proj.layout.buildDirectory.dir('libs')
            dependsOn proj.tasks.named('assemble')
        }
    }

    doLast {
        def copyConfigs = [
            'fabric:1.21.1': '*-signed.jar',
            'fabric:1.21.5': '*-signed.jar',
            'fabric:1.21.10': '*-signed.jar',
            'fabric:1.21.11': '*-signed.jar',
            'fabric:client': '*-signed.jar',
            'neoforge:server': '*.jar',
            'neoforge:client': '*.jar',
            'paper': '*',
            'velton': '*-signed.jar'
        ]
        
        projects.each { projectPath, proj ->
            copy {
                from libDirs[projectPath]
                into layout.buildDirectory.dir('output')
                include copyConfigs[projectPath]
            }
        }
    }
}

tasks.named('build') {
    dependsOn 'copyArtifacts'
}