plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.mklv'
version = rootProject.version

base {
    archivesName = 'hand-shaker-paper'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
    
    // Database dependencies
    implementation 'org.xerial:sqlite-jdbc:3.47.1.0'
    implementation 'com.zaxxer:HikariCP:6.2.1'
    implementation 'org.slf4j:slf4j-api:2.0.16'
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}

processResources {
    dependsOn rootProject.tasks.named('copyCert')
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') { expand(version: project.version) }
}

shadowJar {
    archiveClassifier.set('')

    relocate 'com.zaxxer.hikari', 'me.mklv.handshaker.libs.hikari'
    relocate 'org.sqlite', 'me.mklv.handshaker.libs.sqlite'
    relocate 'org.slf4j', 'me.mklv.handshaker.libs.slf4j'

    dependencies {
        exclude(dependency('io.papermc.paper:paper-api'))
        exclude(dependency('org.xerial:sqlite-jdbc'))
        exclude(dependency('org.slf4j:slf4j-api'))
    }
}

jar {
    enabled = false
}

tasks.named('assemble') {
    dependsOn shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = 'hand-shaker-paper'
        }
    }
}
