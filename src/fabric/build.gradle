plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

subprojects {
    group = rootProject.group
    version = rootProject.version
}

version = project.mod_version + "-" + project.minecraft_version
group = project.maven_group

base {
	archivesName = "${project.archives_base_name}-fabric"
}

repositories {
	// Placeholder
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// Database dependencies - include in JAR
	include implementation('org.xerial:sqlite-jdbc:3.47.1.0')
	include implementation('com.zaxxer:HikariCP:6.2.1')
	include implementation('org.slf4j:slf4j-api:2.0.16')
	
	include(modImplementation("me.lucko:fabric-permissions-api:${fabric_permissions_api}"))
}

tasks.withType(ProcessResources).configureEach {
    dependsOn rootProject.tasks.named('copyCert')
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {

	}
}

afterEvaluate {
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            def (key, value) = line.split('=', 2)
            if (key && value && !System.getenv(key)) {
                System.setProperty(key, value)
            }
        }
    }

    tasks.register('signFabricJar', Exec) {
        group = 'build'
        description = 'Signs the remapped Fabric client JAR.'
        dependsOn(tasks.named('remapJar'))

        def remappedJarProvider = tasks.named('remapJar').flatMap { it.outputs.files.elements.map { fs -> fs.first().asFile } }
        def signedJar = layout.buildDirectory.file("libs/${project.archives_base_name}-fabric-${project.version}-signed.jar")

        inputs.file(remappedJarProvider)
        inputs.file(rootProject.file('Integrity/keystore.jks'))
        outputs.file(signedJar)

        executable = 'jarsigner'

        doFirst {
            def jarToSign = remappedJarProvider.get()
            if (rootProject.file('Integrity/keystore.jks').exists()) {
                println "--> Signing JAR: ${jarToSign.name}"
                def keystorePassword = System.getenv('KEYSTORE_PASSWORD') ?: System.getProperty('KEYSTORE_PASSWORD') ?: 'WDgydwjdh'
                args = [
                    '-keystore', rootProject.file('Integrity/keystore.jks').absolutePath,
                    '-storepass', keystorePassword,
                    '-signedjar', signedJar.get().asFile.absolutePath,
                    jarToSign.absolutePath,
                    'handshaker'
                ]
            } else {
                println "--> Keystore not found in Integrity/keystore.jks. Skipping signing."
                enabled = false
            }
        }
    }

    tasks.named('assemble').configure {
        dependsOn('signFabricJar')
    }
}